import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'signing'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id "me.champeau.gradle.jmh" version "0.5.0"
    id 'net.nemerosa.versioning' version '2.12.1'
}

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

group = 'net.sf.xmldb-org'
version = '2.0.0-SNAPSHOT'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.1'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.openjdk.jmh:jmh-core:1.23'
    
    testRuntimeOnly 'org.mockito:mockito-core:1.10.19'
    testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

signing {
    required { project.hasProperty('signing.password') && !project.version.endsWith('-SNAPSHOT') }
    sign publishing.publications
}

jar {
    manifest {
        attributes 'Implementation-Title': 'XML:DB API specification',
                   'Implementation-Version': project.version,
                   'Build-Date': buildDate,
                   'Build-Time': buildTime,
                   'Build-Revision': versioning.info.commit,
                   'Automatic-Module-Name': "org.xmldb.api"
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}

jmh {
//    include = 'org\\.openjdk\\.jmh\\.samples\\.JMHSample_01_HelloWorld.*'
    humanOutputFile = null
    warmupIterations = 1
    iterations = 1
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped'
    }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'pr', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name  = 'XML:DB API specification'
                description = 'XML:DB Initiative for XML Databases'
                url = 'https://github.com/xmldb-org/xmldb-api.git'
                scm {
                    connection = 'scm:git@github.com:xmldb-org/xmldb-api.git'
                    developerConnection = 'scm:git@github.com:xmldb-org/xmldb-api.git'
                    url = 'https://github.com/xmldb-org/xmldb-api.git'
                }
                licenses {
                    license {
                        name = 'The XML:DB Initiative Software License, Version 1.0'
                        url = 'https://github.com/xmldb-org/xmldb-api/raw/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'reinhapa'
                        name = 'Patrick Reinhart'
                        email = 'patrick@reini.net'
                    }
                    developer {
                        id = 'per_nyfelt'
                        name = 'Per Nyfelt'
                        email = 'per_nyfelt@users.sf.net'
                    }
                    developer {
                        id = 'walkabout'
                        name = 'Lars Martin'
                        email = 'walkabout@users.sf.net'
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}
